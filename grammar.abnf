; The grammar is currently very ambiguous, requiring unrestricted lookahead or backtracking.

fountain-screenplay = *empty-line (script-content / cover-page 1*empty-line script-content)

cover-page = 1*cover-entry

cover-entry = cover-key *space ":" *space cover-value

cover-key = "TITLE" / "CREDIT" / "AUTHOR" / "SOURCE" / "DRAFT DATE" / "CONTACT"

cover-value = single-value / multi-value

single-value = 1*non-newline newline

multi-value = newline 1*indented-sub-value

indented-sub-value = (htab / 2*8(space)) 1*non-newline newline

script-content = *(section-indicator / master-scene / synopse)

section-indicator = 1*"#" *space 1*non-newline newline empty-line

master-scene = master-scene-heading scene-content

master-scene-heading = int-ext scene-description *scene-number *space newline empty-line

int-ext = ("I" (["."] "/" ["E"] / ("NT" ["."] "/EXT")) / "E" ("ST" / "XT")) ("." / space)

scene-description = *space 1*non-newline-or-hash

scene-number = "#" 1*scene-number-character "#" *space


scene-number-character = alphanumeric / "-" / "."

uppercase-letter  = %x41-5A

digit = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"

alphanumeric = alpha / digit

space = " "
      / %x00A0      ; non-breaking
      / %x2000-2009 ; varying-width Em/En-based spaces
      / %x202F      ; narrow non-breaking
      / %x205F      ; mathematical middle-space
      / %x3000)     ; Ideographic space
      ; These are turned into the regular space; we're trying to imitate a typewriter.
      ; Hairline or zero-width spaces and joiners are pre-filtered out previous to parsing.
      ; Same goes for control characters.

newline = cr [lf]
        / lf [cr]
        / %x0B    ; We interpret vertical tabbing as a newline too
        / %x0C    ; As well as form-feeding
        / %x0085  ; Unicode next-line
        / %x2028  ; Unicode line-separator
        / %x2029  ; Unicode paragraph-separator
        ; These are all converted into the POSIX newline "\n".

empty-line = *space newline

character = <any Unicode character>

non-newline = <character - newline>

non-newline-or-hash = <non-newline - "#">
