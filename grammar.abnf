; The grammar is currently very ambiguous, requiring unrestricted lookahead or backtracking.

fountain-screenplay = *empty-line *1cover-page script-content

cover-page = 1*cover-entry

cover-entry = cover-key *space ":" *space cover-value

cover-key = %i"TITLE" / "CREDIT" / "AUTHOR" / "SOURCE" / "DRAFT" 1*space "DATE" / "CONTACT"

cover-value = single-value / multi-value

single-value = 1*non-newline newline

multi-value = newline 1*indented-sub-value

indented-sub-value = 2*8space 1*non-newline newline

script-content = *(section-indicator / master-scene / synopse)

section-indicator = 1*"#" *space 1*non-newline newline empty-line

master-scene = master-scene-heading scene-content

master-scene-heading = int-ext scene-description *scene-number *space newline empty-line

int-ext = ("I" (["."] "/" ["E"] / ("NT" ["."] "/EXT")) / "E" ["XT"]) ("." / space)

scene-description = *space 1*non-newline-or-hash

scene-number = "#" 1*scene-number-character "#" *space

scene-number-character = alphanumeric / "-" / "."

uppercase-letter  = %x41-5A

digit = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"

alphanumeric = alpha / digit

space = " "
      / HTAB        ; tabulator -- converts into 4 spaces
      / %x00A0      ; non-breaking
      / %x2000-2009 ; varying-width Em/En-based spaces
      / %x202F      ; narrow non-breaking
      / %x205F      ; mathematical middle-space
      / %x3000      ; Ideographic space
      ; These are turned into one or more regular spaces; we're trying to imitate a typewriter.
      ; Hairline or zero-width spaces and joiners are pre-filtered out previous to parsing.
      ; Same goes for control characters.

newline = CR [LF]
        / LF [CR]
        / %x0B    ; We interpret vertical tabbing as a newline too
        / %x0C    ; As well as form-feeding
        / %x0085  ; Unicode next-line
        / %x2028  ; Unicode line-separator
        / %x2029  ; Unicode paragraph-separator
        ; These are all converted into your OS's native newline

empty-line = *space newline

character = <any Unicode character>

non-newline = <character - newline>

non-newline-or-hash = <non-newline - "#">
